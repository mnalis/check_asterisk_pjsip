#!/usr/bin/perl -T
# GPLv3+ by Matija Nalis <mnalis-perl@axe.tomsoft.hr> started 2023-01-27
# checks specific Asterisk / Freepbx PJSIP registrations / endpoints

use warnings;
use strict;
use Getopt::Long qw(:config auto_version);

our $VERSION = '1.00';
%ENV = ( PATH => '/usr/local/sbin:/usr/local/bin:/usr/sbin:/usr/bin:/sbin:/bin' );
my $ASTERISK = "/usr/sbin/asterisk";

# no user configurable parts below
my $VALID_NAME_REGEX='[a-zA-Z0-9_\-]+';
my $DEBUG = 0;

my %NAGIOS = (
    STATE_OK => 0,
    STATE_WARNING => 1,
    STATE_CRITICAL => 2,
    STATE_UNKNOWN => 3,
);

my $ret_value = $NAGIOS{'STATE_UNKNOWN'};

# --help
sub print_help() {
    print STDERR <<EOF;
check_asterisk_pjsip $VERSION

Usage: check_asterisk_pjsip [OPTION]...

Options:
  --registration|-r REG1[,REG2]...\t- check registrations
  --endpoint|-e END1[,END2]...\t\t- check endpoints
  --debug|-d 1\t\t\t\t- enable debug
  --help|-h\t\t\t\t- this help
  
Example: check_asterisk_pjsip -r HT1 -r HT2 --endpoint TSMTS
EOF

    exit $NAGIOS{'STATE_UNKNOWN'};
}

# print message (and update status) if one check is OK
sub check_is_ok($) {
    my ($str) = @_;
    print "OK: $str\n";
    $ret_value = $NAGIOS{'STATE_OK'} if $ret_value == $NAGIOS{'STATE_UNKNOWN'};		# only UNKNOWN changes to OK
}

# print message (and update status) if one check is WARN
sub check_is_warn($) {
    my ($str) = @_;
    print "WARNING: $str\n";
    $ret_value = $NAGIOS{'STATE_WARNING'} if $ret_value != $NAGIOS{'STATE_CRITICAL'} ;	# anything but CRITICAL (i.e. UNKNOWN & OK) change to WARNING
}

# print message (and update status) if one check is CRITICAL
sub check_is_crit($) {
    my ($str) = @_;
    print "CRITICAL: $str\n";
    $ret_value = $NAGIOS{'STATE_CRITICAL'};	# all change to CRITICAL
}

# open_asterisk_cmd - executes a given command and returns filehandles to its output $fh
sub open_asterisk_cmd($) {
    my ($cmd) = @_;
    $DEBUG > 2 && print STDERR "Executing $ASTERISK $cmd\n";
    
    open(my $fh, "-|", $ASTERISK, "-x", $cmd) || die "Can't exec: '$ASTERISK -x $cmd': $! ($?)";
    return $fh;
}

### MAIN ###

my $did_something = 0;
my @REGISTRATIONS = ();
my @ENDPOINTS = ();

GetOptions (
    'registration=s' => \@REGISTRATIONS,
    'endpoint=s' => \@ENDPOINTS,
    'debug=i' => \$DEBUG,
    'help' => sub { print_help() }
    );

if ($DEBUG =~ /^(\d+)$/) { $DEBUG = $1 } else { die "invalid param -d $DEBUG" }

# --registration
sub parse_reg($) {
    my ($reg) = @_;
    my $found = 0;
    
    my $fh = open_asterisk_cmd('pjsip show registrations');
    while (<$fh>) {
#  TSMTS/sip:ims.example.com:5060                    TSMTS                       Registered        (exp. 389s)
        $DEBUG > 5 && print STDERR "Reg output: $_";

        if (m{^\s*${reg}/\S*\s+\S+\s+(\S+)}) {
            $found++;
            my $status = $1;
            $DEBUG > 1 && print STDERR " Reg found for $reg; status: $status\n";
            if ($status =~ /registered/i) {
                check_is_ok ("Registration status for $reg is $status")
            } else {
                check_is_crit ("Registration status for $reg is $status")
            }
        }
    }
    if ($found != 1) { check_is_warn ("Found $found registration results for $reg, was expecting only one") }
}
foreach my $reg (@REGISTRATIONS) {
    if ($reg =~ /^(${VALID_NAME_REGEX})$/) { $reg = $1 } else { die "Invalid name in $reg" }
    $DEBUG && print "check registration for: $reg\n";
    parse_reg($reg);
    $did_something++;
}



# --endpoint
sub parse_endp($) {
    my ($endp) = @_;
    my $found = 0;

    my $fh = open_asterisk_cmd('pjsip show endpoints');
    while (<$fh>) {
#       Contact:  HT1/sip:192.168.115.60                     20f051ecf1 Avail        12.243

        $DEBUG > 5 && print STDERR "Endp output: $_";

        if (m{^\s*Contact:\s*${endp}/\S*\s+\S+\s+(\S+)}) {
            $found++;
            my $status = $1;
            $DEBUG > 1 && print STDERR " Endp found for $endp; status: $status\n";
            if ($status =~ /Avail/i) {
                check_is_ok ("Endpoint status for $endp is $status")
            } else {
                check_is_crit ("Endpoint status for $endp is $status")
            }
        }
    }
    if ($found != 1) { check_is_warn ("Found $found endpoint results for $endp, was expecting only one") }
}
foreach my $endp (@ENDPOINTS) {
    if ($endp =~ /^(${VALID_NAME_REGEX})$/) { $endp = $1 } else { die "Invalid name in $endp" }
    $DEBUG && print "check endpoint for: $endp\n";
    parse_endp($endp);
    $did_something++;
}


unless ($did_something) {  # if help requested, or missing required params, show help page
    print_help();
    exit $NAGIOS{'STATE_UNKNOWN'};
}

exit $ret_value;
